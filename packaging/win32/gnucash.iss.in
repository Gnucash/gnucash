; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Input configuration for the Inno Setup Compiler
; Copyright (c) 2004-2005 Christian Stimming <stimming@tuhh.de>
;
; Inno Setup Compiler: See http://www.jrsoftware.org/isdl.php
; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

[Setup]
; Using the name here directly because we want it capitalized
AppName=GnuCash
AppVerName=GnuCash @VERSION@
AppPublisher=GnuCash Development Team
AppPublisherURL=http://www.gnucash.org
AppSupportURL=http://www.gnucash.org
AppUpdatesURL=http://www.gnucash.org
VersionInfoVersion=@VERSION@
DefaultDirName={pf}\@PACKAGE@
DefaultGroupName=GnuCash
LicenseFile=@prefix@\..\dist\share\@PACKAGE@\doc\COPYING
Compression=lzma
MinVersion=5.0,5.0
PrivilegesRequired=poweruser
OutputDir=.
OutputBaseFilename=@PACKAGE@-@VERSION@-setup
UninstallFilesDir={app}\uninstall\@PACKAGE@
InfoAfterFile=@prefix@\..\dist\share\@PACKAGE@\doc\README.win32-bin.txt
SetupIconFile=@prefix@\..\dist\share\@PACKAGE@\pixmaps\gnucash-icon.ico
WizardSmallImageFile=@prefix@\..\dist\share\@PACKAGE@\pixmaps\gnucash-icon-48x48.bmp

[Types]
Name: "full"; Description: "{cm:FullInstall}"
Name: "custom"; Description: "{cm:CustomInstall}"; Flags: iscustom

[Components]
Name: "main"; Description: "{cm:MainFiles}"; Types: full custom; Flags: fixed
;Name: "translations"; Description: "{cm:TranslFiles}"; Types: full
;Name: "templates"; Description: "{cm:TemplFiles}"; Types: full

[Tasks]
Name: desktopicon; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: menuicon; Description: "{cm:CreateMenuLink}"; GroupDescription: "{cm:AdditionalIcons}"

[Icons]
Name: "{group}\GnuCash"; Filename: "{app}\bin\gnucash.cmd"; WorkingDir: "{app}\bin"; Comment: "{cm:IconComment_GnuCash}"; IconFilename: "{app}\share\gnucash\pixmaps\gnucash-icon.ico"; Tasks: menuicon; Flags: runminimized
Name: "{group}\{cm:IconName_README}"; Filename: "{app}\doc\gnucash\{cm:IconFilename_README}"; Comment: "{cm:IconComment_README}"; Tasks: menuicon
Name: "{group}\{cm:IconName_FAQ}"; Filename: "http://wiki.gnucash.org/wiki/FAQ"; Tasks: menuicon
Name: "{group}\{cm:IconName_Bugzilla}"; Filename: "http://bugzilla.gnome.org/enter_bug.cgi?product=GnuCash"; Tasks: menuicon
Name: "{group}\{cm:IconName_InstallFQ}"; Filename: "{app}\bin\install-fq-mods.cmd"; WorkingDir: "{app}\bin"; Comment: "{cm:IconComment_InstallFQ}"; Tasks: menuicon
Name: "{group}\{cm:IconName_Uninstall}"; Filename: "{uninstallexe}"; Comment: "{cm:IconComment_Uninstall}"; Tasks: menuicon

Name: "{commondesktop}\GnuCash"; Filename: "{app}\bin\gnucash.cmd"; WorkingDir: "{app}\bin"; Comment: "{cm:IconComment_GnuCash}"; IconFilename: "{app}\share\gnucash\pixmaps\gnucash-icon.ico"; Tasks: desktopicon; Flags: runminimized

[Run]
Filename: "{app}\bin\gnucash.cmd"; Description: "{cm:RunPrg}"; WorkingDir: "{app}\bin"; OnlyBelowVersion: 0,6; Flags: postinstall skipifsilent runhidden
Filename: "{app}\bin\guile.cmd"; Parameters: "-c ""(use-modules (ice-9 slib)) (require 'printf)"""; Flags: runhidden
;; The Windows firewall exceptions, see http://www.vincenzo.net/isxkb/index.php?title=Adding_a_rule_to_the_Windows_firewall
Filename: "{sys}\netsh.exe"; Parameters: "firewall add allowedprogram ""{app}\bin\gnucash-bin.exe"" ""GnuCash Free Finance Manager"" ENABLE ALL"; StatusMsg: "{cm:StatusMsgFirewall}"; Flags: runhidden; MinVersion: 0,5.01.2600sp2;
Filename: "{sys}\netsh.exe"; Parameters: "firewall add allowedprogram ""{app}\bin\gconfd-2.exe"" ""GConf Settings Manager"" ENABLE ALL"; StatusMsg: "{cm:StatusMsgFirewall}"; Flags: runhidden; MinVersion: 0,5.01.2600sp2;

[UninstallRun]
Filename: "{app}\bin\gconftool-2.exe"; Parameters: "--shutdown"; Flags: runhidden
Filename: "{sys}\netsh.exe"; Parameters: "firewall delete allowedprogram program=""{app}\bin\gnucash-bin.exe"""; Flags: runhidden; MinVersion: 0,5.01.2600sp2
Filename: "{sys}\netsh.exe"; Parameters: "firewall delete allowedprogram program=""{app}\bin\gconfd-2.exe"""; Flags: runhidden; MinVersion: 0,5.01.2600sp2

; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Here we configure the included files and the place of their
; installation
; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[Files]
; The main executables and DLLs
Source: "@prefix@\..\dist\bin\*"; DestDir: "{app}\bin"; Flags: recursesubdirs ignoreversion; Components: main; AfterInstall: MyAfterInstallConfig()
; Note: The above AfterInstall function will create the 
; gnucash.cmd file on-the-fly by the Pascal script below.

Source: "@prefix@\..\dist\etc\*"; DestDir: "{app}\etc"; Flags: recursesubdirs; Components: main
Source: "@prefix@\..\dist\lib\*"; DestDir: "{app}\lib"; Flags: recursesubdirs; Components: main
Source: "@prefix@\..\dist\libexec\*"; DestDir: "{app}\libexec"; Flags: recursesubdirs; Components: main
Source: "@prefix@\..\dist\share\*"; DestDir: "{app}\share"; Flags: recursesubdirs; Components: main

; Additionally, we ship the Qt DLLs because the Online-Banking-Wizard
; lib/aqbanking/plugins/@-aqbanking_so_effective-@/wizards/qt3-wizard.exe needs them.
Source: "@-qtbindir-@\QtCore4.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@-qtbindir-@\QtGui4.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@-qtbindir-@\mingwm10.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
; The following are all needed by Qt3Support
Source: "@-qtbindir-@\Qt3Support4.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@-qtbindir-@\QtXml4.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@-qtbindir-@\QtSql4.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "@-qtbindir-@\QtNetwork4.dll"; DestDir: "{app}\bin"; Flags: ignoreversion

;; The translations
;Source: "@prefix@\..\dist\share\locale\*"; DestDir: "{app}\share\locale"; Flags: recursesubdirs; Components: translations
;
;; The account templates
;Source: "@prefix@\..\dist\share\gnucash\accounts\*"; DestDir: "{app}\share\gnucash\accounts"; Flags: recursesubdirs; Components: templates

; And all the documentation
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\README"; DestDir: "{app}\doc\@PACKAGE@"; Components: main
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\README.win32-bin.txt"; DestDir: "{app}\doc\@PACKAGE@"; Components: main
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\README-de.win32-bin.txt"; DestDir: "{app}\doc\@PACKAGE@"; Components: main
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\README-fr.win32-bin.txt"; DestDir: "{app}\doc\@PACKAGE@"; Components: main
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\README-it.win32-bin.txt"; DestDir: "{app}\doc\@PACKAGE@"; Components: main
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\COPYING"; DestDir: "{app}\doc\@PACKAGE@"; Flags: ignoreversion; Components: main
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\AUTHORS"; DestDir: "{app}\doc\@PACKAGE@"; Components: main
Source: "@prefix@\..\dist\share\@PACKAGE@\doc\ChangeLog"; DestDir: "{app}\doc\@PACKAGE@"; Components: main


; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Define the registry keys Setup should create (HKLM = HKEY_LOCAL_MACHINE)
; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[Registry]
Root: HKCR; Subkey: ".gnucash"; ValueType: string; ValueName: ""; ValueData: "GnuCash.Financial.Data"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".gnucash"; ValueType: string; ValueName: "Content Type"; ValueData: "application/x-gnucash"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "GnuCash.Financial.Data"; ValueType: string; ValueName: ""; ValueData: "GnuCash Financial Data"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "GnuCash.Financial.Data\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\share\gnucash\pixmaps\gnucash-icon.ico,0" 
Root: HKCR; Subkey: "GnuCash.Financial.Data\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\gnucash.cmd"" ""%1""" 

Root: HKLM; Subkey: "Software\GnuCash"; ValueType: none; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\GnuCash\Paths"; ValueType: none; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\GnuCash\Paths"; ValueType: string; ValueName: "prefix"; ValueData: "{app}"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\GnuCash"; ValueType: string; ValueName: "InstallationDirectory"; ValueData: "{app}"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\GnuCash\Paths"; ValueType: string; ValueName: "libdir"; ValueData: "{app}\lib"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\GnuCash\Paths"; ValueType: string; ValueName: "pkglibdir"; ValueData: "{app}\lib\@PACKAGE@"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\GnuCash\Paths"; ValueType: string; ValueName: "sysconfdir"; ValueData: "{app}\etc"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\GnuCash\Paths"; ValueType: string; ValueName: "localedir"; ValueData: "{app}\share\locale"; Flags: uninsdeletevalue

; Additionally, we have to install the paths for gwenhywfar
Root: HKLM; Subkey: "Software\Gwenhywfar"; ValueType: none; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Gwenhywfar\Paths"; ValueType: none; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Gwenhywfar\Paths"; ValueType: string; ValueName: "prefix"; ValueData: "{app}"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Gwenhywfar\Paths"; ValueType: string; ValueName: "libdir"; ValueData: "{app}\lib"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Gwenhywfar\Paths"; ValueType: string; ValueName: "plugindir"; ValueData: "{app}\lib\gwenhywfar\plugins\38"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Gwenhywfar\Paths"; ValueType: string; ValueName: "sysconfdir"; ValueData: "{app}\etc"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Gwenhywfar\Paths"; ValueType: string; ValueName: "localedir"; ValueData: "{app}\share\locale"; Flags: uninsdeletevalue

; And we also need some registry keys for aqbanking
Root: HKLM; Subkey: "Software\Aqbanking"; ValueType: none; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: none; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: string; ValueName: "providerdir"; ValueData: "{app}\lib\aqbanking\plugins\@-aqbanking_so_effective-@\providers"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: string; ValueName: "bankinfodir"; ValueData: "{app}\lib\aqbanking\plugins\@-aqbanking_so_effective-@\bankinfo"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: string; ValueName: "importerdir"; ValueData: "{app}\lib\aqbanking\plugins\@-aqbanking_so_effective-@\imexporters"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: string; ValueName: "wizarddir"; ValueData: "{app}\lib\aqbanking\plugins\@-aqbanking_so_effective-@\wizards"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: string; ValueName: "pkgdatadir"; ValueData: "{app}\share\aqbanking"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: string; ValueName: "sysconfdir"; ValueData: "{app}\etc"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\Aqbanking\Paths"; ValueType: string; ValueName: "localedir"; ValueData: "{app}\share\locale"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\AqBanking\Paths"; ValueType: string; ValueName: "xmldatadir"; ValueData: "{app}\share\aqhbci\xml"; Flags: uninsdeletevalue
Root: HKLM; Subkey: "Software\AqBanking\Paths"; ValueType: string; ValueName: "cfgmoduledir"; ValueData: "{app}\lib\aqbanking\plugins\@-aqbanking_so_effective-@\frontends\qbanking\cfgmodules"; Flags: uninsdeletevalue


; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Delete the created config script on uninstall
; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[UninstallDelete]
Type: files; Name: "{app}\bin\gnucash.cmd"
Type: files; Name: "{app}\bin\guile.cmd"
Type: files; Name: "{app}\share\guile\1.6\slibcat"
Type: filesandordirs; Name: "{app}\share\guile"
Type: filesandordirs; Name: "{app}\etc\gconf"
Type: dirifempty; Name: "{app}\etc"

; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; This large section is a Pascal scripting program that will modify
; the gnucash shell script so that it then includes the
; correct values according to our local installation. See
; http://www.remobjects.com/?ps for a syntax reference.
; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[Code]
function MingwBacksl(const S: String): String;
begin
  { Modify the path name S so that it can be used by MinGW }
  if Length(ExtractFileDrive(S)) = 0 then
    Result := S
  else begin
    Result := '/'+S;
    StringChange(Result, ':\', '\');
  end;
  StringChange(Result, '\', '/');
end;

procedure MyAfterInstallConfig();
var
  FileName, FileString, appdir, libdir, pkglibdir, pkgdatadir: String;
  Res: Boolean;
begin

  { Get the installation-specific paths }
  appdir := ExpandConstant('{app}');
  libdir := appdir + '\lib';
  pkglibdir := libdir + '\gnucash';
  pkgdatadir := appdir + '\share\gnucash';

  { Create the gnucash.cmd file; #10 is the linefeed character and #13 CR }

  { If you make any changes here, you should probably also change the equivalent sections }
  { in packaging/win32/install.sh, src/bin/gnucash-setup-env.in and src/bin/gnucash-setup-env-osx.in }
  FileName := appdir + '\bin\gnucash.cmd' ;
  FileString := 'setlocal'#13#10 ;
  FileString := FileString + 'chcp ' + ExpandConstant('{cm:LanguageCodePage}') + ''#13#10 ;
  FileString := FileString + 'set PATH=' + appdir + '\bin;' + libdir + ';' + libdir + '\gnucash;%PATH%'#13#10 ;

  FileString := FileString + 'set GUILE_WARN_DEPRECATED=no'#13#10 ;
  FileString := FileString + 'set GNC_MODULE_PATH=' + pkglibdir + ''#13#10 ;
  FileString := FileString + 'set GUILE_LOAD_PATH=' + pkgdatadir + '\guile-modules;' + pkgdatadir + '\scm;' + appdir + '\share\guile\1.6;%GUILE_LOAD_PATH%'#13#10 ;
  FileString := FileString + 'set LTDL_LIBRARY_PATH=' + libdir + ''#13#10 ;
  FileString := FileString + 'set QOF_LIB_DIR=' + pkglibdir + ''#13#10 ;
  FileString := FileString + 'set GNC_DBD_DIR=' + libdir + '\dbd'#13#10 ;
  FileString := FileString + 'set GNC_STANDARD_REPORTS_DIR=' + pkgdatadir + '\guile-modules\gnucash\report\standard-reports'#13#10 ;
  FileString := FileString + 'set SCHEME_LIBRARY_PATH='#13#10 ;
  FileString := FileString + 'start gnucash-bin %*'#13#10 ;

  { Save the final file }
  Res := SaveStringToFile(FileName, FileString, False);
  if Res = False then
    MsgBox('Error on saving '+FileName+' for completing the installation', mbInformation, MB_OK);

  FileName := appdir + '\bin\guile.cmd' ;
  FileString := 'set GUILE_LOAD_PATH=' + pkgdatadir + '\guile-modules;' + pkgdatadir + '\scm;' + appdir + '\share\guile\1.6;%GUILE_LOAD_PATH%'#13#10 ;
  FileString := FileString + 'start guile.exe %*'#13#10 ;
  Res := SaveStringToFile(FileName, FileString, False);
  if Res = False then
    MsgBox('Error on saving '+FileName+' for completing the installation', mbInformation, MB_OK);

end;


[Languages]
Name: "de"; MessagesFile: "compiler:Languages\German.isl"; InfoAfterFile: "@prefix@\share\@PACKAGE@\doc\README-de.win32-bin.txt"
Name: "el"; MessagesFile: "@prefix@\share\@PACKAGE@\Greek-4-5.1.11.isl"
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"; InfoAfterFile: "@prefix@\share\@PACKAGE@\doc\README-fr.win32-bin.txt"
Name: "it"; MessagesFile: "compiler:Languages\Italian.isl"; InfoAfterFile: "@prefix@\share\@PACKAGE@\doc\README-it.win32-bin.txt"
Name: "pt_BR"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"

;; The following language files are available as well, in alphabetical
;; order: Basque, BrazilianPortuguese, Catalan, Czech, Danish, Dutch,
;; Finnish, Hungarian, Italian, Norwegian, Polish, Portuguese,
;; Russian, Slovak, Slovenian, Spanish

; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; These are only for improved text messages
; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[Messages]

[CustomMessages]
; *** "Select Components" wizard page
FullInstall=Full installation
CustomInstall=Custom installation
CreateDesktopIcon=Create a &desktop icon
CreateMenuLink=Create a start menu link
RunPrg=Run GnuCash now
AdditionalIcons=Create these icons:
StatusMsgFirewall=Installing Windows firewall rules...

MainFiles=GnuCash Program
TranslFiles=Translation Files
TemplFiles=Account Template Files

IconComment_GnuCash=GnuCash Free Finance Manager
IconName_README=Show README
IconComment_README=Show the README file
IconFilename_README=README.win32-bin.txt
IconName_FAQ=FAQ (Online)
IconName_Bugzilla=Report a Bug (Online)
IconName_InstallFQ=Install Online Price Retrieval
IconComment_InstallFQ=Install the necessary perl module Finance-Quote for online retrieval of prices.  Requires ActivePerl 5.8
IconName_Uninstall=Uninstall GnuCash
IconComment_Uninstall=Uninstall the Finance Manager GnuCash

;; List of Code pages, used in gnucash.cmd
LanguageCodePage=1252
de.LanguageCodePage=1252
el.LanguageCodePage=1253
fr.LanguageCodePage=1252
it.LanguageCodePage=1252
pt_BR.LanguageCodePage=1252


;; ;;;;;;;;;;;;;;;;;;;;
;; German translation

de.FullInstall=Komplett-Installation
de.CustomInstall=Benutzerdefiniert
de.CreateDesktopIcon=Ein Icon auf dem Desktop erstellen
de.CreateMenuLink=Eine Verknόpfung im Startmenό erstellen
de.RunPrg=GnuCash jetzt starten
de.AdditionalIcons=Folgende Icons erstellen:
de.StatusMsgFirewall=Ausnahmen fόr Windows-Firewall installieren...

de.MainFiles=GnuCash Hauptprogramm
de.TranslFiles=Deutsche άbersetzung
de.TemplFiles=Beispiel-Kontenrahmen

de.IconComment_GnuCash=GnuCash OpenSource-Finanzverwaltung
de.IconName_README=README anzeigen
de.IconComment_README=Die Informationsdatei (README) anzeigen
de.IconFilename_README=README-de.win32-bin.txt
de.IconName_FAQ=Hδufige Fragen (online, engl.)
de.IconName_Bugzilla=Fehlerbericht einsenden (online, engl.)
de.IconName_InstallFQ=Online Kursabruf installieren
de.IconComment_InstallFQ=Das zusδtzliche Modul Finance-Quote installieren, um online Aktien- und Devisenkurse abrufen zu kφnnen.  Dazu ist das Programm ActivePerl 5.8 erforderlich.
de.IconName_Uninstall=Deinstallieren von GnuCash
de.IconComment_Uninstall=Die OpenSource-Finanzverwaltung GnuCash wieder deinstallieren und vom Computer lφschen


;; ;;;;;;;;;;;;;;;;;;;
;; Greek translation

el.FullInstall=Πλήρης εγκατάσταση
el.CustomInstall=Προσαρμοσμένη εγκατάσταση
el.CreateDesktopIcon=Δημιουργία εικονιδίου στην επιφάνεια εργασίας
el.CreateMenuLink=Προσθήκη στο μενού Έναρξη
el.RunPrg=Εκτέλεση του GnuCash τώρα
el.AdditionalIcons=Δημιουργία εικονιδίων:
el.StatusMsgFirewall=Εγκατάσταση κανόνων για το τοίχος προστασίας των Windows...

el.MainFiles=Πρόγραμμα GnuCash
el.TranslFiles=Αρχεία μετάφρασεων
el.TemplFiles=Αρχεία με πρότυπα λογαριασμών

el.IconComment_GnuCash=GnuCash πρόγραμμα διαχ. οικονομικών
el.IconName_README=Εμφάνιση του README
el.IconComment_README=Εμφάνιση του αρχείου README
el.IconFilename_README=README.win32-bin.txt
el.IconName_FAQ=Συχνές ερωτήσεις (Online)
el.IconName_Bugzilla=Αναφορά σφάλματος (Online)
el.IconName_InstallFQ=Εγκατάσταση λήψης τιμών (online)
el.IconComment_InstallFQ=Εγκατάσταση του perl module Finance-Quote για λήψη τιμών online.  Απαιτεί ActivePerl 5.8
el.IconName_Uninstall=Απεγκατάσταση GnuCash
el.IconComment_Uninstall=Απεγκατάσταση του διαχειριστή οικονομικών GnuCash


;; ;;;;;;;;;;;;;;;;;;;;
;; French translation

fr.FullInstall=Installation complθte
fr.CustomInstall=Installation personnalisιe
fr.CreateDesktopIcon=Crιer un icτne sur le bureau
fr.CreateMenuLink=Crιer un lien dans le menu de dιmarrage
fr.RunPrg=Dιmarrer GnuCash maintenant
fr.AdditionalIcons=Crιer les icτnes suivants:
fr.StatusMsgFirewall=Installation des rθgles de pare-feu de Windows

fr.MainFiles=Programme GnuCash
fr.TranslFiles=Traduction franηaise
fr.TemplFiles=Modθle de plan comptable

fr.IconComment_GnuCash=GnuCash, le gestionnaire financier OpenSource
fr.IconName_README=Afficher le LISEZMOI
fr.IconComment_README=Afficher le fichier LISEZMOI
fr.IconFilename_README=README-fr.win32-bin.txt
fr.IconName_FAQ=FAQ (En ligne, en anglais)
fr.IconName_Bugzilla=Envoyer un rapport d'erreur (En ligne, en anglais)
fr.IconName_InstallFQ=Installer les quotations en ligne
fr.IconComment_InstallFQ=Installation du module Finance-Quote requis pour le tιlιchargement du cours des devises et actions. Le programme ActivePerl 5.8 est aussi requis
fr.IconName_Uninstall=Dι-installer GnuCash
fr.IconComment_Uninstall=Dιsinstalle le gestionnaire financier GnuCash


;; ;;;;;;;;;;;;;;;;;;;;;
;; Italian translation

it.FullInstall=Installazione completa
it.CustomInstall=Installazione personalizzata
it.CreateDesktopIcon=Crea un'icona sul desktop
it.CreateMenuLink=Crea un collegamento nel menu "start"
it.RunPrg=Avvia GnuCash
it.AdditionalIcons=Crea queste icone:
it.StatusMsgFirewall=Installazione delle regole per il firewall di Windows...

it.MainFiles=File del programma GnuCash
it.TranslFiles=Traduzione Italiana
it.TemplFiles=Modelli di strutture dei conti

it.IconComment_GnuCash=GnuCash: gestore di finanze libero
it.IconName_README=Mostra il file LEGGIMI
it.IconComment_README=Mostra il file LEGGIMI
it.IconFilename_README=README-it.win32-bin.txt
it.IconName_FAQ=FAQ (online, in Inglese)
it.IconName_Bugzilla=Comunicare un bug (online, in Inglese)
it.IconName_InstallFQ=Installa la funzione di ricerca delle quotazioni online
it.IconComment_InstallFQ=Installa il modulo di perl Finance-Quote necessario per ricevere le quotazioni online. Richiede ActivePerl 5.8
it.IconName_Uninstall=Disinstalla GnuCash
it.IconComment_Uninstall=Disinstalla il programma di gestione delle finanze GnuCash


;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Brazilian Portuguese translation

pt_BR.FullInstall=Instalaηγo Completa
pt_BR.CustomInstall=Instalaηγo Personalizada
pt_BR.CreateDesktopIcon=Criar um νcone na Αrea de Trabalho
pt_BR.CreateMenuLink=Criar um link no menu Iniciar
pt_BR.RunPrg=Executar o GnuCash agora
pt_BR.AdditionalIcons=Criar estes νcones:
pt_BR.StatusMsgFirewall=Instalando as regras de firewall do Windows...

pt_BR.MainFiles=Programa GnuCash
pt_BR.TranslFiles=Traduηγo
pt_BR.TemplFiles=Modelos de Conta

pt_BR.IconComment_GnuCash=Gerenciador Financeiro Livre GnuCash
pt_BR.IconName_README=Mostrar LEIA-ME (README)
pt_BR.IconComment_README=Mostra o arquivo LEIA-ME (README)
pt_BR.IconFilename_README=LEIA-ME.win32-bin.txt
pt_BR.IconName_FAQ=Perguntas Freqόentes (online, inglκs)
pt_BR.IconName_Bugzilla=Relatar um erro (online, inglκs)
pt_BR.IconName_InstallFQ=Instalar a Consulta de Preηos Online
pt_BR.IconComment_InstallFQ=Instala o mσdulo perl Finance-Quote, necessαrio para a busca de preηos online. Requer ActivePerl 5.8.
pt_BR.IconName_Uninstall=Desinstalar o GnuCash
pt_BR.IconComment_Uninstall=Desinstala o Gerenciador Financeiro GnuCash

;; ;;;;;;;;;;;;;;;;;;;;
